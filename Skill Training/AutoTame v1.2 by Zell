// This enforces selection of tank on client launch incase the var
// wasn't updated in razor options
if not listexists 'tankcount'
  createlist 'tankcount'
  overhead 'Please select your tanking pet'
  setvar 'tank'
endif

// Bandage timer creation
if not timerexists 'bandageTimer'
  createtimer 'bandageTimer'
  settimer 'bandageTimer' 0
endif

// Bandage my tank every 10 seconds, change as required
if timer 'bandageTimer' > 8000
  @dclicktype 'bandage' backpack
  pause 500
  target 'tank'
  pause 1000
  if insysmsg 'not damaged'
    settimer 'bandageTimer' 4000
  else
    settimer 'bandageTimer' 0
  endif
endif

// In case for some reason bandage doesnt get used on tank
// cancels target so bandage doesnt target grey untamed
hotkey 'Cancel current target'

// Apply pets to tame at specific levels, feel free to add
// them, see user doc for how
if skill 'Taming' < 60
  removelist 'tameables'
  createlist 'tameables'
  pushlist 'tameables' 'a colossal frog'
elseif skill 'Taming' < 70
  removelist 'tameables'
  createlist 'tameables'
  pushlist 'tameables' 'a cave bear'
elseif skill 'Taming' < 75
  removelist 'tameables'
  createlist 'tameables'
  pushlist 'tameables' 'a scarab'
elseif skill 'Taming' < 80 // At 75 taming
  removelist 'tameables'
  createlist 'tameables'
  // Drake Whelp
  pushlist 'tameables' '718'
  // or Aegis Slime
  pushlist 'tameables' '51'
elseif skill 'Taming' < 85 // At 80 taming
  removelist 'tameables'
  createlist 'tameables'
  // Deep Crawler
  pushlist 'tameables' '315'
elseif skill 'Taming' < 90 // At 85 taming
  removelist 'tameables'
  createlist 'tameables'
  // Drake red body
  pushlist 'tameables' '61'
  // Drake grey body
  pushlist 'tameables' '60'
elseif skill 'Taming' < 95 // At 90 taming
  removelist 'tameables'
  createlist 'tameables'
  // Husk Crab
  pushlist 'tameables' '729'
elseif skill 'Taming' < 100 // At 95 taming
  removelist 'tameables'
  createlist 'tameables'
  // or Dragon grey body
  pushlist 'tameables' '12'
  // or Dragon red body
  pushlist 'tameables' '59'
  // Dragon whelp
  //pushlist 'pets' '718'
elseif skill 'Taming' < 105 // At 100 taming
  removelist 'tameables'
  createlist 'tameables'
  // Snow Drift
  pushlist 'tameables' '51'
elseif skill 'Taming' < 110 // At 105 taming, Earth Drake
  removelist 'tameables'
  createlist 'tameables'
  // Drake grey body
  pushlist 'tameables' '60'
  // Drake red body
  pushlist 'tameables' '61'
  // Acarid
  //pushlist 'pets' '385'
elseif skill 'Taming' < 115 // At 110 taming
  removelist 'tameables'
  createlist 'tameables'
  // Aegis Minion
  pushlist 'tameables' '776'
  // or Sphinx
  pushlist 'tameables' '293'
elseif skill 'Taming' < 120 // At 115 taming, Earth Dragon
  removelist 'tameables'
  createlist 'tameables'
  // Grey Dragon body value
  pushlist 'tameables' '12'
  // Red Dragon body value
  pushlist 'tameables' '59'
  // or Colossal Sandroach
  pushlist 'tameables' '388'
else
  overhead 'STOP! You have finished your journey' "14"
  pause 1000
  replay
endif
clearsysmsg

foreach tameable in tameables
  if findtype tameable true
    overhead "Tameable in range..." "232"
    hotkey "Set Last Target"
    wft
    targettype tameable true

    // open left click menu on last target and try to click 
    // "transfer" to figure out if pet is tamed or not. Pet 
    // context items will differ if you have herding or not
    if skill 'herding' > 0
      menu 'last' 11
    else
      menu 'last' 10
    endif

    pause 1000
    hotkey 'Cancel current target'

    // last target is tamed... kill it.
    if insysmsg 'person to transfer ownership to'
      overhead "Killing tamed pet..." "232"
      //melee it to help kill faster
      attack "last"

      // use pet context menu to attack nearest tameable
      if skill 'herding' > 0
        menu 'tank' 2
      else
        menu 'tank' 1
      endif
      wft
      // pause 2500
      target "last"
      break
    else
      overhead 'Attempting to tame...' "232"
      if not timerexists 'tamingTimer'
        createtimer 'tamingTimer'
        settimer 'tamingTimer' 0
      endif

      if timer 'tamingTimer' > 13000
        useskill 'Taming'
        pause 750
        @target 'last'
        settimer 'tamingTimer' 0
      endif

    endif

  elseif findtype tameable
      overhead "No tameable in range, fetching nearest tameable" "232"
      pause 550

      overhead "setting last target"
      hotkey "Set Last Target"
      wft
      targettype tameable
      
      // use pet context menu to attack nearest tameable
      if skill 'herding' > 0
        menu 'tank' 2
      else
        menu 'tank' 1
      endif

      wft 1000
      lasttarget
      //wait for pet to get to target
      pause 3000
      // use pet context menu to bring pet back with tameable
      if skill 'herding' > 0
        menu 'tank' 6
      else
        menu 'tank' 5
      endif

      //toggle war/peace so we don't autoswing at it and interupt taming
      hotkey 'Toggle War/Peace'
      hotkey 'Toggle War/Peace'

      //wait for pet to get back to us so we don't pull a second
      pause 3000
  endif
endfor
loop